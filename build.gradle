group 'jp.co.fablic.spikot'
version '0.1.0'

buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.8'
    }
}

apply plugin: 'kotlin'
apply plugin: 'de.fuerstenau.buildconfig'
apply plugin: 'idea'

buildConfig {
    appName = project.name
    packageName = project.group
    clsName = 'BuildConfig'

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    buildConfigField 'String', 'SLACK_TOKEN', properties.getProperty("slackToken", null)
    buildConfigField 'String', 'SLACK_CHANNEL_NAME', properties.getProperty("slackChannelName", null)
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'http://jcenter.bintray.com' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.github.kittinunf.fuel:fuel:1.11.0"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19.3"
    compile "org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT"
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}
